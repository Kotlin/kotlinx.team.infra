project.version = project.hasProperty('releaseVersion') ? project.property('releaseVersion') : project.version

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava.sourceDirectories
}

publishing {
    repositories {
        maven {
            name = 'build'
            url = "$rootProject.buildDir/maven"
        }
        maven {
            name = 'space'
            url "https://maven.pkg.jetbrains.space/kotlin/p/kotlinx/maven"
            credentials {
                username = project.findProperty("space.user")
                password = project.findProperty("space.token")
            }
        }
    }

    publications.all {
        it.artifact tasks.sourceJar
    }
}

afterEvaluate {
    clean {
        publishing.repositories.all { repository ->
            if (repository.name == 'build') {
                delete += repository.url
            }
        }
    }

    task('publishToBuildRepository') { publish ->
        group = 'publishing'
        tasks.withType(PublishToMavenRepository) { task ->
            if (task.repository.name == 'build') {
                publish.dependsOn(task)
            }
        }
    }
}

